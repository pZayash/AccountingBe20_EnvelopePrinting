
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ОбъектыНазначения") Тогда
		ОбъектыПечати = Параметры.ОбъектыНазначения;
		Если ОбъектыПечати.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru='Непосредственное открытие этой формы не предусмотрено.'");
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
	Иначе
		//ОбъектыПечати = Новый Массив;
		//ОбъектыПечати.Добавить(Документы.РеализацияТоваровУслуг.НайтиПоНомеру("ФХ00-000238",ТекущаяДата()));
	КонецЕсли;
	
	СведенияОПолучателеКонверта = Параметры.СведенияОПолучателеКонверта;
	Организация = Параметры.Организация;
	
	Если ОбъектыПечати <> Неопределено Тогда
		ПроверитьВозможностьПечати(ОбъектыПечати);
	КонецЕсли;
	
	ФорматыПочтовыхКонвертов = Новый Структура;
	ФорматыПочтовыхКонвертов.Вставить("C4");
	ФорматыПочтовыхКонвертов.Вставить("C5");
	ФорматыПочтовыхКонвертов.Вставить("DL");
	
		Если ОбъектыПечати <> Неопределено Тогда
			ОбъектПечати = ОбъектыПечати[0];
			ВОбъектеПечатиЕстьОрганизация = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ОбъектПечати, "Организация");
			
		Иначе
			ОбъектПечати = Справочники.Контрагенты.ПустаяСсылка();
			ВОбъектеПечатиЕстьОрганизация = Ложь;
			
		КонецЕсли; 
		Элементы.ОбъектПечати.Доступность = Не ЗначениеЗаполнено(ОбъектПечати);
		Элементы.ОбъектПечати.Видимость = ОбъектыПечати = Неопределено Или ОбъектыПечати.Количество() <= 1;
		
		ИспользуетсяОднаОрганизация = Не Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
		
		Если ИспользуетсяОднаОрганизация Тогда
			Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		КонецЕсли;
		
		АдресОбъектовПечати = ПоместитьВоВременноеХранилище(ОбъектыПечати, УникальныйИдентификатор);
		
		НадписьФормат = НСтр("ru='Формат:'");
		Элементы.Организация.Видимость = Истина;
		Элементы.ВидАдресаКонтрагента.Видимость = НЕ ЗначениеЗаполнено(СведенияОПолучателеКонверта);
	
	ФорматКонверта = "C5";
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// 20240105 Заяш
	// В БП.Бел.21 нет наименования брузера
	// НаименованиеБраузера = РегламентированнаяОтчетностьКлиент.ВебБраузер();
	НаименованиеБраузера = "";
	
	Если ЗначениеЗаполнено(НаименованиеБраузера) Тогда
		Элементы.ГруппаОсобенностиПечатиВВебКлиенте.Видимость = Истина;
		СообщитьПользователюОсобенностиПечатиВВебБраузере(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НеТребуютПроверки = Новый Массив;
	
	НеТребуютПроверки.Добавить("ВидАдресаКонтрагента");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НеТребуютПроверки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФорматКонвертаС4ПриИзменении(Элемент)
	
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматКонвертаС5ПриИзменении(Элемент)
	
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматКонвертаDLПриИзменении(Элемент)
	
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонвертC4Нажатие(Элемент)
	
	ФорматКонверта = "C4";
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонвертC5Нажатие(Элемент)
	
	ФорматКонверта = "C5";
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонвертDLНажатие(Элемент)
	
	ФорматКонверта = "DL";
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечататьЛоготипРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Организация) И (ИспользуетсяОднаОрганизация Или Элементы.Организация.Видимость) Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, Организация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПродолжить(Команда)
	
	ОбъектыПечати = ПолучитьОбъектыПечати();
	
	Если Не ЗначениеЗаполнено(ОбъектыПечати) Тогда
		Возврат;
	КонецЕсли;
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ПечатьКонвертов");
	
	ИмяМакета = ИмяМакета();
	
	ПараметрыПечати = Новый Структура();
	ПараметрыПечати.Вставить("ФорматКонверта"      , ФорматКонверта);
	ПараметрыПечати.Вставить("ПечататьЛоготип"     , ПечататьЛоготип);
	ПараметрыПечати.Вставить("ВидАдресаКонтрагента", ВидАдресаКонтрагента);
	ПараметрыПечати.Вставить("Организация"         , Организация);
	ПараметрыПечати.Вставить("ИмяМакета"           , ИмяМакета);
	ПараметрыПечати.Вставить("ЗаголокФормы"        , НСтр("ru='Печать конверта'"));
	Если ЗначениеЗаполнено(СведенияОПолучателеКонверта) Тогда
		ПараметрыПечати.Вставить("СведенияОПолучателеКонверта", СведенияОПолучателеКонверта);
	КонецЕсли;
	
	ТабличныйДокумент = КомандаПродолжитьНаСервере(ПараметрыПечати, ОбъектыПечати, ОбъектыПечати);
	ТабличныйДокумент.Показать();
	
	Закрыть();
	
КонецПроцедуры

Функция КомандаПродолжитьНаСервере(ПараметрыПечати, ОбъектыПечати, МассивОбъектов);
	
	ОбъектОбработка = РеквизитФормыВЗначение("Объект");
	
	Результат = ОбъектОбработка.ПечатьКонверта(МассивОбъектов, ОбъектыПечати, ПараметрыПечати);
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ИмяМакета()
	
	Результат = "";
	
	Если ФорматКонверта = "C4" Тогда
		Результат = "ПФ_MXL_КонвертC4";
	ИначеЕсли ФорматКонверта = "C5" Тогда
		Результат = "ПФ_MXL_КонвертС5";
	ИначеЕсли ФорматКонверта = "DL" Тогда
		Результат = "ПФ_MXL_КонвертDL";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ФорматКонвертаПриИзменении(Форма)
	
	ФорматыПочтовыхКонвертов = Форма.ФорматыПочтовыхКонвертов;
	ФорматКонверта           = Форма.ФорматКонверта;
	Элементы                 = Форма.Элементы;
	
	Для Каждого Формат Из ФорматыПочтовыхКонвертов Цикл
		ЭтоВыбранныйФормат = Формат.Ключ = ФорматКонверта;
		Элементы["Конверт" + Формат.Ключ].Видимость = Не ЭтоВыбранныйФормат;
		Элементы["Конверт" + Формат.Ключ + "Выбран"].Видимость = ЭтоВыбранныйФормат;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Форма.НаименованиеБраузера) Тогда
		СообщитьПользователюОсобенностиПечатиВВебБраузере(Форма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВозможностьПечати(ОбъектыПечати)
	
	ОбъектыПечати = УдалитьГруппыИзОбъектовПечати(ОбъектыПечати);
	Если ОбъектыПечати.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Конверт может быть напечатан только для элементов справочника.'");
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Функция УдалитьГруппыИзОбъектовПечати(ОбъектыПечати)
	
	Если ТипЗнч(ОбъектыПечати[0]) <> Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат ОбъектыПечати;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбъектыПечати", ОбъектыПечати);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка В(&ОбъектыПечати)";
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

&НаСервере
Функция ПолучитьОбъектыПечати()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПолучитьИзВременногоХранилища(АдресОбъектовПечати);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СообщитьПользователюОсобенностиПечатиВВебБраузере(Форма)
	
	РазмерБумаги = РазмерБумаги(Форма);
	
	Если Форма.НаименованиеБраузера = "CHROME" Тогда
		ТекстСообщения = НСтр("ru='На следующем шаге, в окне предпросмотра, выберите размер бумаги'") + " " + РазмерБумаги;
		
	ИначеЕсли Форма.НаименованиеБраузера = "MSIE" Тогда
		ТекстСообщения = НСтр("ru='На следующем шаге выберите в Параметрах страницы размер бумаги'") + " " + РазмерБумаги;
		
	Иначе
		ТекстСообщения = НСтр("ru='На следующем шаге выберите в Свойствах принтера размер бумаги'") + " " + РазмерБумаги;
		
	КонецЕсли;
	
	Форма.Элементы.ТекстОсобенностиПечатиВВебКлиенте.Заголовок = ТекстСообщения;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РазмерБумаги(Форма)
	
	Если Форма.ФорматКонверта = "C4" Тогда
		Результат = "Envelope C4";
	ИначеЕсли Форма.ФорматКонверта = "C5" Тогда
		Результат = "Envelope C5";
	Иначе
		Результат = "Envelope DL";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


// 20240105 Заяш
&НаСервере
Процедура ОбъектПечатиПриИзмененииНаСервере()
	
	массивОбъектовПечати = Новый Массив;
	массивОбъектовПечати.Добавить(ОбъектПечати);
	
	АдресОбъектовПечати = ПоместитьВоВременноеХранилище(массивОбъектовПечати, УникальныйИдентификатор);

КонецПроцедуры

// 20240105 Заяш
&НаКлиенте
Процедура ОбъектПечатиПриИзменении(Элемент)
	ОбъектПечатиПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти
